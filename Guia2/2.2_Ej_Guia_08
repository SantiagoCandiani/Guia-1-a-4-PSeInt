//Un docente de Programación tiene un listado de 3 notas registradas por cada uno de sus N estudiantes.
//La nota final se compone de un trabajo práctico Integrador (35%), una Exposición (25%) y un Parcial (40%).

//El docente requiere los siguientes informes claves de sus estudiantes:
//		- Nota promedio final de los estudiantes que reprobaron el curso. Un estudiante reprueba el curso si tiene una nota final inferior a 6.5
//		- Porcentaje de alumnos que tienen una nota de integrador mayor a 7.5.
//		- La mayor nota obtenida en las exposiciones.
//		- Total de estudiantes que obtuvieron en el Parcial entre 4.0 y 7.5.

////		El programa pedirá la cantidad de alumnos que tiene el docente y en cada alumno pedirá las 3 notas y calculará todos informes claves que requiere el docente.

Algoritmo sin_titulo
	
	Definir nEst, cont1, cont2,cont3, i Como Entero
	Definir notFinal, sumaFinal, not1, not2, not3, mayor Como Real
	
	Escribir "Ingrese el numero de estudiantes"
	leer nEst
	
	i=1
	cont1=0 // contador de desaprobados
	cont2=0 // contador de alumnos con integrador mayor a 7.5
	cont3=0 //contador de alumnos que obtuvieron en el Parcial entre 4.0 y 7.5
	nEst=nEst
	mayor=0

	sumaFinal= 0
	
	Para i=1 Hasta nEst Con Paso 1 Hacer
		Escribir "Estudiante " i ": Ingrese la nota del Trabajo Practico Integrador " Sin Saltar
		leer not1
		Escribir "Estudiante " i ": Ingrese la nota de la Exposición " Sin Saltar
		leer not2
		Escribir "Estudiante " i ": Ingrese la nota del parcial " Sin Saltar
		leer not3
		
		notFinal = (not1*0.35) + (not2*0.25) + (not3*0.40)
		
		si notFinal<6.5 Entonces 
			cont1 = cont1 + 1					//cantidad de desaprobados
			sumaFinal = sumaFinal + notFinal	//suma de notas menores a 6.5 para sacar el promedio
		FinSi
		
		si not1>7.5 Entonces 
			cont2 = cont2 + 1					//cantidad de alumnos con integrador mayor a 7.5
		FinSi
		
		si not2>mayor Entonces
			mayor=not2
		FinSi
		
		si not3>=4 y not3<=7.5 Entonces
			cont3 = cont3 + 1
		FinSi
		
	Fin Para
	
	Si cont1=0 Entonces
		Escribir "No hubo estudiantes desprobados"
	SiNo
		Escribir  "La nota promedio final de los estudiantes que reprobaron el curso es " (sumaFinal/cont1)
	Fin Si
	
	Si cont2=0 Entonces
		Escribir "No hubo estudiantes con nota de integrador mayor a 7.5"
	SiNo
		Escribir  "El porcentaje de alumnos que tienen una nota de integrador mayor a 7.5 es del " (cont2*100)/nEst "%"
	Fin Si
	
	Escribir "La mayor nota obtenida en las exposiciones es " mayor
	
	Si cont3=0 Entonces
		Escribir "No hubo estudiantes con nota en el Parcial entre 4.0 y 7.5"
	SiNo
		Escribir  "El total de estudiantes que obtuvieron en el Parcial entre 4.0 y 7.5 fue de: " cont3 " alumnos"
	Fin Si
	
FinAlgoritmo
